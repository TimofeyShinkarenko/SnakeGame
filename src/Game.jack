class Game {
    static int delay;
    static Snake snake;
    static Apple apple;
    static Score score;
    static Random random;
    static int segmentSize;
    static int seed;
    static boolean exitGame;

    function void startAllGame() {
        let segmentSize = 9;
        let delay = 100;
        let score = Score.new();
        let seed = 12345;
        let exitGame = false;
        
        while (~exitGame)
        {            
            do Game.drawStartMenu();
            do Game.waitForStart();
            do Game.startGame();
        }
        return;
    }

    function void startGame() {
        var Segment nextHeadSegment;
        var Segment head;
      
        do score.setPoints(3);  
        let snake = Snake.new(67, 20, score.getPoints(), segmentSize);
        let random = Random.new(seed);
        let apple = Apple.createRandomApple(random, segmentSize);
   
        do Game.drawGameField();
        
        while (~(snake.IsColliding()) & ~(exitGame)) {
            do snake.move();
            
            // check if apple is eaten            
            let head = snake.getHeadSegment();
            let nextHeadSegment = snake.calculateNewSegment(
                head.getX1(), head.getY1(), 
                head.getX2(), head.getY2()
            );

            do Game.HandleAppleEating(nextHeadSegment);
            let exitGame = Game.checkExit();           
            do Sys.wait(delay);
        }
        if (exitGame) {
            do Game.exitGame();
            return;
        }
        do Game.drawGameOver();
        do snake.dispose();      
   
        return;
    }

    function void HandleAppleEating(Segment nextHeadSegment) {
        if (apple.isEaten(nextHeadSegment.getX1(), nextHeadSegment.getY1())) {
                // snake is growing adding next segment
                do apple.erase();
                do snake.addSegment();
                do score.addPoints();
                do score.drowScore();
                
                // add new apple
                let apple = Apple.createRandomApple(random, segmentSize);
                do apple.draw();
                
                // redraw snake's head (cause it might have been deleted)
                do snake.drawHead();
            }
        return;
    }

    function void drawGameField() {
        do Screen.clearScreen();
        do Output.moveCursor(3, 1);
        do Output.printString("Score:");   
        do score.drowScore();     
        do MyScreen.drawRectangle(63, 16, 449, 240, 4);
        do apple.draw();
        return;
    }

    function void drawGameOver() {
        do Screen.clearScreen();
        do Output.moveCursor(12, 25);
        do Output.printString("Game Over!");
        do Sys.wait(1000);
        return;
    }
  
    function void drawStartMenu() {
        var int snakeLength, i, x, y, segmentSpacing;

        do Screen.clearScreen();

        // Display "Press Space to Start" message
        do Output.moveCursor(11, 20);
        do Output.printString("Press Space to Start");
        
        do Screen.drawLine(20, 10, 492, 10);
        do Screen.drawLine(20, 246, 492, 246);
        do Screen.drawLine(10, 20, 10, 236);
        do Screen.drawLine(502, 20, 502, 236);

        do MyScreen.drawRectangle(140, 115, 332, 141, 3);

        do Output.moveCursor(15, 18);
        do Output.printString("Best result: ");
        do Output.printInt(score.getBestResult());
        do Output.printString(" !");

        return;
    }
    
    function void waitForStart() {
        var char key;
        var boolean gameStarted;
        let gameStarted = false;
        
        while (~gameStarted) {
            let seed = seed + 1;
            let key = Keyboard.keyPressed();
            if (key = 32) {     // space
                let gameStarted = true;
            }
            do Sys.wait(50);
        }
        return;
    }

    function boolean checkExit() {
        var char key;
        let key = Keyboard.keyPressed();
        return (key = 140);
    }

    function void exitGame() {
        do Screen.clearScreen();
        do Output.moveCursor(12, 25);
        do Output.printString("Game Exited!");
        do Sys.wait(1000);
        return;
    }
}
